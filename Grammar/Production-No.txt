S' -> S $ 1
S -> <Body> $ 2
S -> <Body> S $ 3
<Body> -> <Decl> $ 4
<Decl> -> <VarDecl> $ 5
<Decl> -> <FuncDecl> $ 6
<VarDecl> -> int ID ; $ 7
<VarDecl> -> int ID = <Exprsn> ; $ 8
<VarDecl> -> float ID ; $ 9
<VarDecl> -> float ID = <Exprsn> ; $ 10
<FuncDecl> -> <FuncHead> <StmtBlock> $ 11
<FuncHead> -> int ID ( <FormalParams> ) $ 12
<FuncHead> -> float ID ( <FormalParams> ) $ 13
<FuncHead> -> void ID ( <FormalParams> ) $ 14
<FormalParams> -> <ParamList> $ 15
<FormalParams> -> void $ 16
<FormalParams> -> ε $ 17
<ParamList> -> <Param> $ 18
<ParamList> -> <Param> , <ParamList> $ 19
<Param> -> int ID $ 20
<Param> -> float ID $ 21
<StmtBlock> -> { <Stmts> } $ 22
<Stmts> -> <Stmt> <Stmts> $ 23
<Stmts> -> <Stmt> $ 24
<Stmt> -> <VarDecl> $ 25
<Stmt> -> <IfStmt> $ 26
<Stmt> -> <WhileStmt> $ 27
<Stmt> -> <ReturnStmt> $ 28
<Stmt> -> <AssignStmt> $ 29
<Stmt> -> ID <FuncCall> ; $ 30
<AssignStmt> -> ID = <Exprsn> ; $ 31
<ReturnStmt> -> return <Exprsn> ; $ 32
<ReturnStmt> -> return ; $ 33
<WhileStmt> -> while ( <Exprsn> ) <StmtBlock> $ 34
<IfStmt> -> if ( <Exprsn> ) <StmtBlock> else <StmtBlock> $ 35
<IfStmt> -> if ( <Exprsn> ) <StmtBlock> $ 36
<Exprsn> -> <AddExprsn> $ 37
<Exprsn> -> <AddExprsn> < <Exprsn> $ 38
<Exprsn> -> <AddExprsn> <= <Exprsn> $ 39
<Exprsn> -> <AddExprsn> > <Exprsn> $ 40
<Exprsn> -> <AddExprsn> >= <Exprsn> $ 41
<Exprsn> -> <AddExprsn> == <Exprsn> $ 42
<Exprsn> -> <AddExprsn> != <Exprsn> $ 43
<AddExprsn> -> <Item> + <AddExprsn> $ 44
<AddExprsn> -> <Item> - <AddExprsn> $ 45
<AddExprsn> -> <Item> $ 46
<Item> -> <Factor> * <Item> $ 47
<Item> -> <Factor> / <Item> $ 48
<Item> -> <Factor> $ 49
<Factor> -> inum $ 50
<Factor> -> - inum $ 51
<Factor> -> fnum $ 52
<Factor> -> - fnum $ 53
<Factor> -> ( <Exprsn> ) $ 54
<Factor> -> ID $ 55
<Factor> -> ID <FuncCall> $ 56
<FuncCall> -> ( <ActualArgs> ) $ 57
<ActualArgs> -> <ArgList> $ 58
<ActualArgs> -> void $ 59
<ActualArgs> -> ε $ 60
<ArgList> -> <Exprsn> , <ArgList> $ 61
<ArgList> -> <Exprsn> $ 62